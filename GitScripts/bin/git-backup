#!/usr/bin/perl

$| = 1;

use strict;
use warnings;

my $remote = shift || die "No remote?";
my $prefix = shift || die "No prefix?";

my %remote_branches;
{
    open(my $fh, "-|", "git", "ls-remote", "--heads", $remote) || die "Cannot open ls-remote";
    while(my $l = <$fh>)
    {
        chomp $l;
        if($l =~ /^([0-9a-f]{40})\t(.*)$/)
        {
            my ($hash, $ref) = ($1, $2);
            if($ref =~ /^refs\/heads\/\Q$prefix\E\/(.*)$/)
            {
                my $name = $1;
                $remote_branches{$name} = $hash;
            }
        }
        else
        {
            die "Bad line in ls-remote: $l";
        }
    }
    close($fh) || die "Cannot close ls-remote";
}

my %local_branches;
{
    open(my $fh, "-|", "git", "branch", "-v", "--no-abbrev") || die "Cannot open branch";
    while(my $l = <$fh>)
    {
        chomp $l;
        $l =~ s/^..//;
        next if($l =~ /^\(no branch\)/);
        if($l =~ /^([^ ]*) *([0-9a-f]{40}) .*$/)
        {
            my ($name, $hash) = ($1, $2);
            $local_branches{$name} = $hash;
        }
        else
        {
            die "Bad line in branche: $l";
        }
    }
    close($fh) || die "Cannot close branch";
}

my @specs;
{
    for my $name (keys(%local_branches))
    {
        my $remote_hash = delete $remote_branches{$name};
        my $local_hash = $local_branches{$name};
        if(!defined($remote_hash))
        {
            print "New branch $name at $local_hash\n";
        }
        elsif($remote_hash ne $local_hash)
        {
            print "Old version of $name is $remote_hash, will update to $local_hash.\n";
        }
        else
        {
            #print "Matching version of $name at $local_hash, will do nothing.\n";
            next;
        }
        push @specs, "refs/heads/$name:refs/heads/$prefix/$name";
    }

    for my $name (keys(%remote_branches))
    {
        my $remote_hash = $remote_branches{$name};
        print "Stale branch $name at $remote_hash, will delete.\n";
        push @specs, ":refs/heads/$prefix/$name";
    }
}

if(@specs)
{
    print "Execing push for update...\n";
    exec("git", "push", $remote, "--force", @specs);
}
else
{
    print "Everything is up to date.\n";
}
